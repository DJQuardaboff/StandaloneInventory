apply plugin: 'com.android.application'
apply from: 'systems.gradle'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 12
        versionName "2.1"
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    flavorDimensions "system", "scannerSdk"
    productFlavors {
        janam_XM5 {
            dimension "scannerSdk"
        }
        janam_XT2_XT2__XM75_XT100_XG200 {
            dimension "scannerSdk"
        }
        zebra_emdk_symbol_26 {
            dimension "scannerSdk"
        }
    }
    productFlavors.whenObjectAdded {
        flavor ->
            flavor.dimension = 'system'
            def flavorData = project[flavor.name]
            flavor.applicationId "com.porterlee.${flavor.name.toLowerCase()}.inventory"
            flavor.resValue 'string', 'app_name', "${flavor.name} Inventory"

            // BarcodeType item
            flavor.buildConfigField 'boolean',  'typeItem_hasCustodyOf', flavorData.typeItem_hasCustodyOf ? 'true' : 'false'
            flavor.buildConfigField 'boolean',  'typeItem_hasLabCode', flavorData.typeItem_hasLabCode ? 'true' : 'false'
            flavor.buildConfigField 'String',   'typeItem_base32Prefix', flavorData.typeItem_base32Prefix ? "\"${flavorData.typeItem_base32Prefix}\"" : 'null'
            flavor.buildConfigField 'String',   'typeItem_base64Prefix', flavorData.typeItem_base64Prefix ? "\"${flavorData.typeItem_base64Prefix}\"" : 'null'
            flavor.buildConfigField 'String[]', 'typeItem_otherPrefixes', flavorData.typeItem_otherPrefixes ? "{\"${flavorData.typeItem_otherPrefixes.join('", "')}\"}" : 'null'
            // BarcodeType item

            // BarcodeType container
            flavor.buildConfigField 'boolean',  'typeContainer_hasCustodyOf', flavorData.typeContainer_hasCustodyOf ? 'true' : 'false'
            flavor.buildConfigField 'boolean',  'typeContainer_hasLabCode', flavorData.typeContainer_hasLabCode ? 'true' : 'false'
            flavor.buildConfigField 'String',   'typeContainer_base32Prefix', flavorData.typeContainer_base32Prefix ? "\"${flavorData.typeContainer_base32Prefix}\"" : 'null'
            flavor.buildConfigField 'String',   'typeContainer_base64Prefix', flavorData.typeContainer_base64Prefix ? "\"${flavorData.typeContainer_base64Prefix}\"" : 'null'
            flavor.buildConfigField 'String[]', 'typeContainer_otherPrefixes', flavorData.typeContainer_otherPrefixes ? "{\"${flavorData.typeContainer_otherPrefixes.join('", "')}\"}" : 'null'
            // BarcodeType container

            // BarcodeType location
            flavor.buildConfigField 'boolean',  'typeLocation_hasCustodyOf', flavorData.typeLocation_hasCustodyOf ? 'true' : 'false'
            flavor.buildConfigField 'boolean',  'typeLocation_hasLabCode', flavorData.typeLocation_hasLabCode ? 'true' : 'false'
            flavor.buildConfigField 'String',   'typeLocation_base32Prefix', flavorData.typeLocation_base32Prefix ? "\"${flavorData.typeLocation_base32Prefix}\"" : 'null'
            flavor.buildConfigField 'String',   'typeLocation_base64Prefix', flavorData.typeLocation_base64Prefix ? "\"${flavorData.typeLocation_base64Prefix}\"" : 'null'
            flavor.buildConfigField 'String[]', 'typeLocation_otherPrefixes', flavorData.typeLocation_otherPrefixes ? "{\"${flavorData.typeLocation_otherPrefixes.join('", "')}\"}" : 'null'
            // BarcodeType location

            flavor.buildConfigField 'boolean',  'display_quantity', flavorData.display_quantity ? 'true' : 'false'
    }
    productFlavors {
        LIMS {}
        LAM {}
        EMS {}
        QM {}
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    output ->
                        outputFileName = "v${versionName}/${variant.productFlavors[0].name}Inventory.apk"
                }
        }
    }
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    janam_XM5Implementation project(':plc_scanners-janam_XM5Release')
    janam_XT2_XT2__XM75_XT100_XG200Implementation project(':plc_scanners-janam_XT2_XT2__XM75_XT100_XG200Release')
    zebra_emdk_symbol_26Implementation project(':plc_scanners-zebra_emdk_symbol_26Release')

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-core-utils:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation('me.zhanghai.android.materialprogressbar:library:1.4.2')     {
        exclude group: 'com.android.support'
        //exclude module: 'appcompat-v7'
    }
    implementation 'org.jetbrains:annotations-java5:15.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}