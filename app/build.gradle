apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.porterlee.standardinventory"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 9
        versionName "1.8"
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    flavorDimensions "system", "scannerSdk"
    productFlavors {
        Default {
            dimension "system"
            resValue "string", "app_name", "Mobile Inventory"
        }
        LAM {
            dimension "system"
            resValue "string", "app_name", "LAM Inventory"
            applicationIdSuffix ".lam"
        }
        janam_XM5 {
            dimension "scannerSdk"
        }
        janam_XT2_XT2__XM75_XT100_XG200 {
            dimension "scannerSdk"
        }
        zebra_emdk_symbol_26 {
            dimension "scannerSdk"
        }
        applicationVariants.all {
            variant ->
                android.productFlavors.all {
                    flavor ->
                        for (int i = 0; i < flavorDimensionList.size(); i++) {
                            if (flavor.dimension == flavorDimensionList[i]) {
                                variant.buildConfigField 'boolean', 'is_' + flavor.getName() + "_" + flavor.dimension, String.valueOf(flavor.name == variant.productFlavors[i].getName())
                            }
                        }
                }
        }
    }
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                output ->
                    if (output.baseName.startsWith("LAM")) {
                        outputFileName = "v${variant.versionName}/LAMInventory.apk"
                    } else {
                        outputFileName = "v${variant.versionName}/StandardInventory.apk"
                    }
            }
    }
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    janam_XM5Implementation project(':plc_scanners-janam_XM5Release')
    janam_XT2_XT2__XM75_XT100_XG200Implementation project(':plc_scanners-janam_XT2_XT2__XM75_XT100_XG200Release')
    zebra_emdk_symbol_26Implementation project(':plc_scanners-zebra_emdk_symbol_26Release')

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-core-utils:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation('me.zhanghai.android.materialprogressbar:library:1.4.2')     {
        exclude group: 'com.android.support'
        //exclude module: 'appcompat-v7'
    }
    implementation 'org.jetbrains:annotations-java5:15.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}